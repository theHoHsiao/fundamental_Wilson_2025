metadata_query = "Nc == {Nc} & Nt == {Nt} & Ns == {Ns} & beta == {beta} & nF == {nF} & mF == {mF}"
dir_template = "Sp{Nc}b{beta}nF{nF}mF{mF}T{Nt}L{Ns}"
plot_styles = "styles/prd.mplstyle"
plot_filetype = "pdf"


include: "rules/gradient_flow.smk"
include: "rules/package.smk"
include: "rules/mpcac.smk"
include: "rules/mass.smk"
include: "rules/plaquette.smk"
include: "rules/combined_plots.smk"
include: "rules/spectrum_plots.smk"
include: "rules/spectrum_tables.smk"
include: "rules/extrapolation.smk"
include: "rules/csvs.smk"


h5_data = expand(
    "data_assets/{datafile}.h5",
    datafile=["corr_sp4_FUN", "nf2_gflow"],
)

topology_history_plots = [
    f"top_charge_history_{ensemble}"
    for ensemble in [
        "Sp4b6.9nF2mF-0.85T32L16",
        "Sp4b6.9nF2mF-0.87T32L16",
        "Sp4b6.9nF2mF-0.89T32L16",
        "Sp4b6.9nF2mF-0.91T32L16",
        "Sp4b6.9nF2mF-0.924T32L24",
        "Sp4b6.9nF2mF-0.92T32L24",
        "Sp4b6.9nF2mF-0.9T32L16",
        "Sp4b7.05nF2mF-0.835T36L20",
        "Sp4b7.05nF2mF-0.857T36L32",
        "Sp4b7.05nF2mF-0.85T36L24",
        "Sp4b7.05nF2mF-0.863T36L36",
        "Sp4b7.05nF2mF-0.867T36L36",
        "Sp4b7.2nF2mF-0.76T36L16",
        "Sp4b7.2nF2mF-0.77T36L24",
        "Sp4b7.2nF2mF-0.78T36L24",
        "Sp4b7.2nF2mF-0.794T36L28",
        "Sp4b7.2nF2mF-0.799T40L32",
        "Sp4b7.2nF2mF-0.79T36L24",
        "Sp4b7.2nF2mF-0.803T42L36",
        "Sp4b7.2nF2mF-0.808T48L36",
        "Sp4b7.2nF2mF-0.808T48L42",
        "Sp4b7.2nF2mF-0.813T48L36",
        "Sp4b7.4nF2mF-0.74T48L36",
        "Sp4b7.4nF2mF-0.755T48L42",
        "Sp4b7.4nF2mF-0.75T48L32",
        "Sp4b7.5nF2mF-0.71T48L36",
        "Sp4b7.5nF2mF-0.72T48L36",
    ]
]
plots = expand(
    f"assets/plots/{{plot}}.{plot_filetype}",
    plot=[
        #"mpcac_vs_m0",
        #"plaquette_phasediagram",
        "w0_vs_mpcac",
        *topology_history_plots,
        "dec2_all_con_sp4fund",
        "dec2_all_lat_a_sp4fund",
        "m2_all_con_sp4fund",
        "m2_all_lat_a_sp4fund",
        "mpsL",
        "Rvps_all_con_sp4fund",

    ],
)
tables = expand(
    "assets/tables/{table}.tex",
    table=[
        "plaquette_table",
        "ensemble",
        "gflow_table",
        "nlo_coefficients_decayconstant",
        "nlo_coefficients_mass",
        "autocorr_table",
        "spectrum_meson_f",
        ],
)
definitions = expand(
    "assets/definitions/{definition}.tex",
    definition=[
        "gflow_incomplete_ensembles",
        "plaquette_phasediagram_betas",
        "spectrum_plots_target_beta",
        "chipt_ratio_direct",
        "chipt_beta_values",
        "finite_volume_definitions",
        "heavy_ps_limit",
    ],
)
csv_data = expand(
    "data_assets/{csv}.csv",
    csv=["ensemble_data", "chipt_deft_data", "global_eft_data"],
)
external_data = expand(
    "external_data/{filename}",
    filename=["mv_fps_fund.csv", "meson_meta_fund.csv", "m2v_fit_pms.json"],
)

rule all:
    input:
        assets="assets/info.json",
        #data_assets="data_assets/info.json",


rule provenance_stamp:
    params:
        module=lambda wildcards, input: input.script.replace("/", ".")[:-3],
    input:
        script="src/provenance.py",
        h5_data=h5_data,
        plots=plots,
        tables=tables,
        ensemble_metadata="metadata/ensemble_metadata.csv",
        external_data=external_data,
    output:
        stamp="assets/info.json",
    conda:
        "envs/flow_analysis.yml"
    shell:
        "python -m {params.module} {input.h5_data} {input.plots} {input.tables} {input.ensemble_metadata} {input.external_data} --output_file {output.stamp}"


rule data_provenance_stamp:
    params:
        module=lambda wildcards, input: input.script.replace("/", ".")[:-3],
    input:
        script="src/provenance.py",
        h5_data=h5_data,
        csv_data=csv_data,
        ensemble_metadata="metadata/ensemble_metadata.csv",
        external_data=external_data,
    output:
        stamp="data_assets/info.json",
    conda:
        "envs/flow_analysis.yml"
    shell:
        "python -m {params.module} {input.h5_data} {input.csv_data} {input.ensemble_metadata} {input.external_data} --output_file {output.stamp}"
